
%start doc

%token        YAML_ANCHOR YAML_ALIAS YAML_TRANSFER YAML_TAGURI YAML_ITRANSFER
%token        YAML_WORD YAML_PLAIN YAML_BLOCK
%token        YAML_DOCSEP YAML_IOPEN YAML_INDENT YAML_IEND

%type         doc basic_seq
%type         atom word_rep ind_rep struct_rep atom_or_empty empty
%type         implicit_seq inline_seq implicit_map inline_map inline_seq_atom inline_map_atom
%type         top_imp_seq in_implicit_seq in_inline_seq basic_mapping complex_key complex_value
%type         top_imp_map in_implicit_map in_inline_map complex_mapping

%left               '-' ':'
%left               '[' ']' '{' '}' ',' '?'

%package org.yecht
%class YechtParser
%next lexer.yylex()
%get  lexer.currentToken()
%semantic Object: lexer.getLVal()

%%

doc     : atom
        {
           parser.root = parser.addNode((Node)$1);
        }
        | YAML_DOCSEP atom_or_empty
        {
           parser.root = parser.addNode((Node)$2);
        }
        |
        {
           parser.eof = true;
        }
        ;

atom	: word_rep
        | ind_rep
        ;

ind_rep : struct_rep
        | YAML_TRANSFER ind_rep
        { 
            Parser.addTransfer((String)$1, (Node)$2, parser.taguri_expansion);
            $$ = $2;
        }
        | YAML_TAGURI ind_rep
        {
            Parser.addTransfer((String)$1, (Node)$2, false);
            $$ = $2;
        }
        | YAML_ANCHOR ind_rep
        { 
           /*
            * _Anchors_: The language binding must keep a separate symbol table
            * for anchors.  The actual ID in the symbol table is returned to the
            * higher nodes, though.
            */
           $$ = parser.addAnchor((String)$1, (Node)$2 );
        }
        | indent_open ind_rep indent_flex_end
        {
           $$ = $2;
        }
        ;

atom_or_empty   : atom
                | empty
                ;

empty           : indent_open empty indent_end
                {
                    $$ = $2;
                }
                |
                {
                    Node n = NULL_NODE( parser );
                    $$ = n;
                }
                | YAML_ITRANSFER empty
                { 
                   if(parser.implicit_typing)
                   {
                      ImplicitScanner.tryTagImplicit((Node)$2, parser.taguri_expansion);
                   }
                   $$ = $2;
                }
                | YAML_TRANSFER empty
                { 
                    Parser.addTransfer((String)$1, (Node)$2, parser.taguri_expansion);
                    $$ = $2;
                }
                | YAML_TAGURI empty
                {
                    Parser.addTransfer((String)$1, (Node)$2, false);
                    $$ = $2;
                }
                | YAML_ANCHOR empty
                { 
                   /*
                    * _Anchors_: The language binding must keep a separate symbol table
                    * for anchors.  The actual ID in the symbol table is returned to the
                    * higher nodes, though.
                    */
                   $$ = parser.addAnchor((String)$1, (Node)$2 );
                }
                ;

/*
 * Indentation abstractions
 */
indent_open     : YAML_IOPEN
                | indent_open YAML_INDENT
                ;
                
indent_end      : YAML_IEND
                ;

indent_sep      : YAML_INDENT
                ;

indent_flex_end : YAML_IEND
                | indent_sep indent_flex_end
                ;

/*
 * Words are broken out to distinguish them
 * as keys in implicit maps and valid elements
 * for the inline structures
 */
word_rep	: YAML_TRANSFER word_rep						
            { 
               Parser.addTransfer((String)$1, (Node)$2, parser.taguri_expansion);
               $$ = $2;
            } 
            | YAML_TAGURI word_rep
            { 
               Parser.addTransfer((String)$1, (Node)$2, false);
               $$ = $2;
            } 
            | YAML_ITRANSFER word_rep						
            { 
               if(parser.implicit_typing)
               {
                  ImplicitScanner.tryTagImplicit((Node)$2, parser.taguri_expansion);
               }
               $$ = $2;
            }
            | YAML_ANCHOR word_rep
            { 
               $$ = parser.addAnchor((String)$1, (Node)$2 );
            }
            | YAML_ALIAS										
            {
               /*
                * _Aliases_: The anchor symbol table is scanned for the anchor name.
                * The anchor's ID in the language's symbol table is returned.
                */
               $$ = parser.getAnchor((String)$1);
            }
			| YAML_WORD
            { 
               Node n = (Node)$1;
               if(parser.taguri_expansion) {
                   n.type_id = Parser.taguri(YAML.DOMAIN, "str");
               } else {
                   n.type_id = "str";
               }
               $$ = n;
            }
            | YAML_PLAIN
            | indent_open word_rep indent_flex_end
            {
               $$ = $2;
            }
            ;

/*
 * Any of these structures can be used as
 * complex keys
 */
struct_rep	: YAML_BLOCK
			| implicit_seq
			| inline_seq
			| implicit_map
			| inline_map
            ;

/*
 * Implicit sequence 
 */
implicit_seq	: indent_open top_imp_seq indent_end
                { 
                    $$ = $2;
                }
                | indent_open in_implicit_seq indent_end
                { 
                    $$ = $2;
                }
                ;

basic_seq       : '-' atom_or_empty             
                { 
                    $$ = parser.addNode((Node) $2);
                }
                ;

top_imp_seq     : YAML_TRANSFER indent_sep in_implicit_seq
                { 
                    Parser.addTransfer((String)$1, (Node)$3, parser.taguri_expansion);
                    $$ = $3;
                }
                | YAML_TRANSFER top_imp_seq
                { 
                    Parser.addTransfer((String)$1, (Node)$2, parser.taguri_expansion);
                    $$ = $2;
                }
                | YAML_TAGURI indent_sep in_implicit_seq
                { 
                    Parser.addTransfer((String)$1, (Node)$3, false);
                    $$ = $3;
                }
                | YAML_TAGURI top_imp_seq
                { 
                    Parser.addTransfer((String)$1, (Node)$2, false);
                    $$ = $2;
                }
                | YAML_ANCHOR indent_sep in_implicit_seq
                { 
                    $$ = parser.addAnchor((String)$1, (Node)$3 );
                }
                | YAML_ANCHOR top_imp_seq
                { 
                    $$ = parser.addAnchor((String)$1, (Node)$2 );
                }
                ;

in_implicit_seq : basic_seq
                {
                    $$ = parser.newSeq(((Long)$1).longValue());
                }
				| in_implicit_seq indent_sep basic_seq
				{ 
                    ((Node)$1).seqAdd(((Long)$3).longValue());
                    $$ = $1;
				}
				| in_implicit_seq indent_sep
				{ 
                    $$ = $1;
				}
                ;

/*
 * Inline sequences
 */
inline_seq		: '[' in_inline_seq ']'
                { 
                    $$ = $2;
                }
				| '[' ']'
                { 
                    $$ = parser.allocSeq();
                }
                ;

in_inline_seq   : inline_seq_atom
                {
                    $$ = parser.newSeq( parser.addNode((Node)$1));
                }
                | in_inline_seq ',' inline_seq_atom
				{ 
                    ((Node)$1).seqAdd(parser.addNode((Node)$3));
                    $$ = $1;
				}
                ;

inline_seq_atom : atom
                | basic_mapping
                ;

/*
 * Implicit maps
 */
implicit_map	: indent_open top_imp_map indent_end
                { 
                    applySeqInMap(parser, (Node)$2);
                    $$ = $2;
                }
                | indent_open in_implicit_map indent_end
                { 
                    applySeqInMap(parser, (Node)$2);
                    $$ = $2;
                }
                ;

top_imp_map     : YAML_TRANSFER indent_sep in_implicit_map
                { 
                    Parser.addTransfer((String)$1, (Node)$3, parser.taguri_expansion);
                    $$ = $3;
                }
                | YAML_TRANSFER top_imp_map
                { 
                    Parser.addTransfer((String)$1, (Node)$2, parser.taguri_expansion);
                    $$ = $2;
                }
                | YAML_TAGURI indent_sep in_implicit_map
                { 
                    Parser.addTransfer((String)$1, (Node)$3, false);
                    $$ = $3;
                }
                | YAML_TAGURI top_imp_map
                { 
                    Parser.addTransfer((String)$1, (Node)$2, false);
                    $$ = $2;
                }
                | YAML_ANCHOR indent_sep in_implicit_map
                { 
                    $$ = parser.addAnchor((String)$1, (Node)$3);
                }
                | YAML_ANCHOR top_imp_map
                { 
                    $$ = parser.addAnchor((String)$1, (Node)$2);
                }
                ;

complex_key     : word_rep
                | '?' atom indent_sep
                {
                    $$ = $2;
                }
                ;

complex_value   : atom_or_empty
                ;

/* Default needs to be added to SyckSeq i think...
				| '=' ':' atom
				{
					result = [ :DEFAULT, val[2] ]
				}
*/

complex_mapping : complex_key ':' complex_value
                {
                    $$ = parser.newMap( 
                        parser.addNode( (Node)$1 ), 
                        parser.addNode( (Node)$3 ) );
                }
/*
				| '?' atom
                {
                    NULL_NODE( parser, n );
                    $$ = syck_new_map( 
                        syck_hdlr_add_node( (SyckParser *)parser, $2 ), 
                        syck_hdlr_add_node( (SyckParser *)parser, n ) );
                }
*/
                ;

in_implicit_map : complex_mapping
				| in_implicit_map indent_sep basic_seq
                {
                    if ( ((Node)$1).shortcut == null )
                    {
                        ((Node)$1).shortcut = parser.newSeq( ((Long)$3).longValue() );
                    }
                    else
                    {
                        ((Node)((Node)$1).shortcut).seqAdd( ((Long)$3).longValue() );
                    }
                    $$ = $1;
                }
				| in_implicit_map indent_sep complex_mapping
                {
                    applySeqInMap( parser, (Node)$1 );
                    ((Node)$1).mapUpdate((Node)$3);
                    $3 = null;
                    $$ = $1;
                }
				| in_implicit_map indent_sep
                {
                    $$ = $1;
                }
                ;

/*
 * Inline maps
 */
basic_mapping	: atom ':' atom_or_empty
                {
                    $$ = parser.newMap( 
                        parser.addNode( (Node)$1 ), 
                        parser.addNode( (Node)$3 ) );
                }
                ;

inline_map		: '{' in_inline_map '}'
                {
                    $$ = $2;
                }
          		| '{' '}'
                {
                    $$ = parser.allocMap();
                }
                ;
         
in_inline_map	: inline_map_atom
				| in_inline_map ',' inline_map_atom
				{
                    ((Node)$1).mapUpdate((Node)$3);
                    $3 = null;
                    $$ = $1;
				}
                ;

inline_map_atom : atom
                {
                    Node n = NULL_NODE( parser );
                    $$ = parser.newMap( 
                        parser.addNode( (Node)$1 ), 
                        parser.addNode( n ) );
                }
                | basic_mapping
                ;

%%

     private Parser parser;
     private Scanner lexer;
     public YechtParser(Parser parser, Scanner lexer) {
       this.parser = parser;
       this.lexer  = lexer;
     }

public static Node NULL_NODE(Parser parser) {
    Node n = parser.newStr(Pointer.create(new byte[0], 0), 0, ScalarStyle.Plain);
    if(parser.taguri_expansion) {
      n.type_id = Parser.taguri(YAML.DOMAIN, "null");
    } else {
      n.type_id = "null";
    }
    return n;
}


public static void applySeqInMap(Parser parser, Node n) {
       if(n.shortcut == null) {
         return;
       }

       int map_len = (int)n.mapCount();
       n.mapAssign(MapPart.Value, map_len - 1, parser.addNode((Node)n.shortcut));
       n.shortcut = null;
}

public void yyerror(String msg) {
       System.err.println("PARSER ERROR: " + msg);
}
